# define name of installer
!define MYSOURCE "../../temp"
OutFile "${MYSOURCE}/Server-Installer.exe"
!include sections.nsh

;--------------------------------
;Include Modern UI
  !include "MUI2.nsh"
  !define MUI_ABORTWARNING

  !define MUI_ICON "../../icon.ico"
  !define MUI_UNICON "../../icon.ico"

  !define MUI_BGCOLOR 222222
  !define MUI_HEADERIMAGE
  !define MUI_HEADERIMAGE_BITMAP "header.bmp"
  !define MUI_HEADERIMAGE_UNBITMAP "header.bmp"
;--------------------------------
 
# define installation directory
InstallDir "$APPDATA\FileFlows"
Icon "../../icon.ico"
Name "FileFlows"
LicenseData "eula.rtf"
SetCompressor /SOLID lzma
!define REGPATH_UNINSTSUBKEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${NAME}"

# For removing Start Menu shortcut in Windows 7
RequestExecutionLevel user
;--------------------------------
;Languages
 
  !insertmacro MUI_LANGUAGE "English"

;--------------------------------
;Version Information

  VIProductVersion "0.6.0.1234"
  VIAddVersionKey /LANG=${LANG_ENGLISH} "ProductName" "FileFlows"
  VIAddVersionKey /LANG=${LANG_ENGLISH} "CompanyName" "Reven"
  VIAddVersionKey /LANG=${LANG_ENGLISH} "LegalCopyright" "Copyright FileFlows 2022"
  VIAddVersionKey /LANG=${LANG_ENGLISH} "FileDescription" "FileFlows"
  VIAddVersionKey /LANG=${LANG_ENGLISH} "FileVersion" "0.6.0"

;--------------------------------

;--------------------------------
;Pages

  #!insertmacro MUI_PAGE_WELCOME
  !insertmacro MUI_PAGE_LICENSE "eula.rtf"
  !insertmacro MUI_PAGE_COMPONENTS
  !insertmacro MUI_PAGE_DIRECTORY
  !insertmacro MUI_PAGE_INSTFILES
  #!insertmacro MUI_PAGE_FINISH
  
  #!insertmacro MUI_UNPAGE_WELCOME
  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_INSTFILES
  #!insertmacro MUI_UNPAGE_FINISH
;--------------------------------


Section "FileFlows Server" g1o1
    ServerInstall:

    IfFileExists "$INSTDIR\Node" 0 +2
        MessageBox MB_OK "FileFlows Node is already installed.  You cannot install both the Server and Node on the same machine." IDOK ServerDone

    IfFileExists $INSTDIR\ServerFileFlows.Server.dll 0 +3
        Delete "$INSTDIR\Server\FileFlows.Server.dll"
        IfErrors ServerAppRunning
 
        # set the installation directory as the destination for the following actions
        SetOutPath $INSTDIR\Server

        File /r "${MYSOURCE}/Server/Plugins"
        File /r "${MYSOURCE}/Server/Nodes"
        File /r "${MYSOURCE}/Server/Templates"
        File /r "${MYSOURCE}/Server/wwwroot"
        File "${MYSOURCE}/Server/"

        SetOutPath $INSTDIR\FlowRunner
        File /r "${MYSOURCE}/FlowRunner"
        
        SetOutPath $INSTDIR\Server\runtimes
        File /r "${MYSOURCE}/Server/runtimes/win*"
        SetOutPath $INSTDIR\Server
    
        # create the uninstaller
        WriteUninstaller "$INSTDIR\Server\uninstall.exe"
        WriteRegStr HKLM "${REGPATH_UNINSTSUBKEY}" "DisplayName" "FileFlows Server"
        WriteRegStr HKLM "${REGPATH_UNINSTSUBKEY}" "DisplayIcon" "$InstDir\Server\icon.ico"
        WriteRegStr HKLM "${REGPATH_UNINSTSUBKEY}" "UninstallString" '"$InstDir\uninstall.exe"'
        WriteRegStr HKLM "${REGPATH_UNINSTSUBKEY}" "QuietUninstallString" '"$InstDir\uninstall.exe" /S'

        #IfFileExists "$SMPROGRAMS\FileFlows" 0 EndIf
        CreateDirectory "$SMPROGRAMS\FileFlows"
        #EndIf:
    
        # create a shortcut named "new shortcut" in the start menu programs directory
        # point the new shortcut at the program uninstaller
        CreateShortcut "$SMPROGRAMS\FileFlows\FileFlows Uninstall.lnk" "$INSTDIR\Server\uninstall.exe"

        # create a shortcut named "new shortcut" in the start menu programs directory
        CreateShortcut "$SMPROGRAMS\FileFlows\FileFlows.lnk" "dotnet.exe"" FileFlows.Server.dll" "$INSTDIR\Server\icon.ico"
        CreateShortcut "$INSTDIR\FileFlows.lnk" "dotnet.exe"" FileFlows.Server.dll" "$INSTDIR\Server\icon.ico"

        ExecShell "" "$SMPROGRAMS\FileFlows\FileFlows.lnk"
        Goto ServerDone

    ServerAppRunning:
        MessageBox MB_RETRYCANCEL "FileFlows is currently running, please stop it and try again." IDCANCEL ServerDone
        Goto ServerInstall

    ServerDone:
SectionEnd

Section /o "FileFlows Node" g1o2

    NodeInstall:
    
        IfFileExists "$INSTDIR\Server" 0 +2
            MessageBox MB_OK "FileFlows Server is already installed.  You cannot install both the Server and Node on the same machine." IDOK NodeDone

        IfFileExists $INSTDIR\FileFlows.Node.dll 0 +3
            Delete "$INSTDIR\FileFlows.Node.dll"
            IfErrors NodeAppRunning

        # set the installation directory as the destination for the following actions
        SetOutPath "$INSTDIR"
        File "${MYSOURCE}/Node/*.bat"

        SetOutPath $INSTDIR\Node
        File "${MYSOURCE}/Node/Node/*"

        SetOutPath $INSTDIR\FlowRunner
        File /r "${MYSOURCE}/FlowRunner"

        SetOutPath $INSTDIR\Node\runtimes
        File /r "${MYSOURCE}/Server/runtimes/win*"
        SetOutPath $INSTDIR\Node
    
        # create the uninstaller
        WriteUninstaller "$INSTDIR\Node\uninstall.exe"
        WriteRegStr HKLM "${REGPATH_UNINSTSUBKEY}" "DisplayName" "FileFlows Node"
        WriteRegStr HKLM "${REGPATH_UNINSTSUBKEY}" "DisplayIcon" "$InstDir\Node\icon.ico"
        WriteRegStr HKLM "${REGPATH_UNINSTSUBKEY}" "UninstallString" '"$InstDir\uninstall.exe"'
        WriteRegStr HKLM "${REGPATH_UNINSTSUBKEY}" "QuietUninstallString" '"$InstDir\uninstall.exe" /S'
    
        CreateDirectory "$SMPROGRAMS\FileFlows"


        # create a shortcut named "new shortcut" in the start menu programs directory
        # point the new shortcut at the program uninstaller
        CreateShortcut "$SMPROGRAMS\FileFlows\FileFlows Node Uninstall.lnk" "$INSTDIR\Node\uninstall.exe"

        # create a shortcut named "new shortcut" in the start menu programs directory
        CreateShortcut "$SMPROGRAMS\FileFlows\FileFlows Node.lnk" "dotnet.exe"" FileFlows.Node.dll" "$INSTDIR\Node\icon.ico"
        CreateShortcut "$INSTDIR\FileFlows Node.lnk" "dotnet.exe"" FileFlows.Node.dll" "$INSTDIR\Node\icon.ico"

        ExecShell "" "$SMPROGRAMS\FileFlows\FileFlows Node.lnk"
        Goto NodeDone

    NodeAppRunning:
        MessageBox MB_RETRYCANCEL "FileFlows Node is currently running, please stop it and try again." IDCANCEL NodeDone
        Goto NodeInstall

    NodeDone:
SectionEnd
 
# uninstaller section start
Section "uninstall"
 
    Uninstall:
        # delete this file, if it fails the app is running
        IfFileExists $INSTDIR\Server\FileFlows.Server.dll 0 +2
            Delete "$INSTDIR\Server\FileFlows.Server.dll"
            IfErrors UninstallAppRunning
            
        IfFileExists $INSTDIR\Node\FileFlows.Node.dll 0 +2
            Delete "$INSTDIR\Node\FileFlows.Node.dll"
            IfErrors UninstallAppRunning

        DeleteRegKey HKLM "${REGPATH_UNINSTSUBKEY}"

        RMDir /r "$SMPROGRAMS\FileFlows"

        MessageBox MB_YESNO|MB_ICONQUESTION "Would you like to remove all data?" IDNO UninstallMinimal
            RMDir /r "$INSTDIR"
            Goto UninstallDone
        
    UninstallMinimal:
        IfFileExists "$INSTDIR\Server" 0 +1
            RMDir /r "$INSTDIR\Server"   
            
        IfFileExists "$INSTDIR\Node" 0 +1
            RMDir /r "$INSTDIR\Node"   
            
        RMDir /r "$INSTDIR\FlowRunner"    
        Goto UninstallDone

    UninstallAppRunning:
        MessageBox MB_RETRYCANCEL "FileFlows is currently running, please stop it and try again." IDCANCEL UninstallDone
        Goto Uninstall

    UninstallDone:
# uninstaller section end
SectionEnd

;--------------------------------
;Descriptions

  ;Language strings
  LangString DESC_g1o1 ${LANG_ENGLISH} "Install the main FileFlows server.  This is all that is needed for a basic FileFlows installation."
  LangString DESC_g1o2 ${LANG_ENGLISH} "Install an external processing node that will connect to an existing FileFlows server."

  ;Assign language strings to sections
  !insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
    !insertmacro MUI_DESCRIPTION_TEXT ${g1o1} $(DESC_g1o1)
    !insertmacro MUI_DESCRIPTION_TEXT ${g1o2} $(DESC_g1o2)
  !insertmacro MUI_FUNCTION_DESCRIPTION_END

;--------------------------------

;--------------------------------

; Functions

; $1 stores the status of group 1
; $2 stores the status of group 2

Function .onInit

  StrCpy $1 ${g1o1} ; Group 1 - Option 1 is selected by default

FunctionEnd

Function .onSelChange

  !insertmacro StartRadioButtons $1
    !insertmacro RadioButton ${g1o1}
    !insertmacro RadioButton ${g1o2}
  !insertmacro EndRadioButtons

FunctionEnd
